COMMENT: "#" /[^\n]/*
PRIMITIVE.2: "str" | "int" | "float" | "bool" | "any"
TYPEDEF.2: "typedef"
HTTP_METHOD.2: "GET"i | "HEAD"i | "POST"i | "PUT"i | "DELETE"i | "CONNECT"i | "OPTIONS"i | "TRACE"i | "PATCH"i
REQUEST.2: "->"
RESPONSE.2: "<-"
SEPARATOR.2: ":"
OPTIONAL.2: "?"
WILDCARD.2: "*"
ARRAY.2: "[]"
IDENTIFIER: /[a-zA-Z][-a-zA-Z0-9_]*/
STATUS_CODE: /[0-9]+/
CONST_VALUE: /[^\n]+/

?start          : _NL* file _NL*
file            : block*
block           : communication _NL*
                | typedef _NL*
                | constant _NL*
constant        : IDENTIFIER "=" CONST_VALUE _NL
communication   : IDENTIFIER _NL _BEGIN constant* request+ _END
request         : HTTP_METHOD _NL _BEGIN request_def response_def _END
request_def     : REQUEST _NL body?
response_def    : RESPONSE _NL _BEGIN response+ _END
response        : STATUS_CODE _NL body?
body            : _BEGIN constant* attribute* _END
attribute       : OPTIONAL? (IDENTIFIER | WILDCARD) ARRAY? SEPARATOR type
type            : primitive | enum | global_type | object
primitive       : PRIMITIVE _NL (_BEGIN constant* _END)?
enum            : IDENTIFIER "{" IDENTIFIER ("," IDENTIFIER)* "}" _NL
object          : IDENTIFIER _NL body?
global_type     : "$" IDENTIFIER _NL
typedef         : TYPEDEF IDENTIFIER primitive      -> typedef_primitive
                | TYPEDEF IDENTIFIER global_type    -> alias
                | TYPEDEF enum                      -> typedef_enum
                | TYPEDEF object                    -> typedef_object

%declare _BEGIN _END

_NL: /(\r?\n[\t ]*)+/

%import common.WS_INLINE
%ignore WS_INLINE
%ignore COMMENT